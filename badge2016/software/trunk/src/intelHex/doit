#!/bin/csh -vf

# this script is to generate the hex files
# with a unique ID in each one. It find a
# key: BEEF in the master hex file and 
# replaces it with loop id 0 -> 400
# which **has** to be in hex

# flashedBadgeId is the variable in the badge used for the unique ID
# grep flashedBadgeId ../../Badge16.X/mappit

# see if BEEF magic appears somewhere else
# __there can be only one__
set count = `grep -i beef ../../Badge16.X/dist/default/production/Badge16.X.production.hex|wc -l`
if ($count != 1) then
   echo "MORE THAN ONE MAGIC KEY IN HEX"
   exit 0
endif

./badgeId.awk badgeId=01E0 < ../../Badge16.X/dist/default/production/Badge16.X.production.hex > t.hex

set diffCount = `diff -i t.hex ../../Badge16.X/dist/default/production/Badge16.X.production.hex|wc -l`
if ($diffCount != 4) then
   echo "DIFF PROBLEM"
   diff -i t.hex ../../Badge16.X/dist/default/production/Badge16.X.production.hex
   exit 0
endif

set checkErrors = `./chkihex.awk ../../Badge16.X/dist/default/production/Badge16.X.production.hex | grep ERROR | wc -l`
if ($checkErrors != 0) then
   echo "CHECK ERRORS "
   ./chkihex.awk t.hex | grep ERROR
   exit 0
endif

#../pic32usb t.hex

set i = 1
while ($i < 400)
   set badgeId = `echo $i | awk '{printf "%04x", $1 }'`
   echo $i is hex $badgeId

   cat ../../Badge16.X/dist/default/production/Badge16.X.production.hex \
   | ./badgeId.awk badgeId=$badgeId \
   > FIX_badge_$i.hex

    @ i++
end



